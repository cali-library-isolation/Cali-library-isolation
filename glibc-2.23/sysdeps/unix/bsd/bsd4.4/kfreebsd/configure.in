GLIBC_PROVIDES dnl See aclocal.m4 in the top level source directory.
# Local configure fragment for sysdeps/unix/bsd/bsd4.4/kfreebsd.

# The kFreeBSD headers can be found in
#   /usr/src/sys/
# Check whether this directory is available.
if test -z "$sysheaders" &&
   test "x$cross_compiling" = xno &&
   test -d /usr/src/sys/ ; then
  sysheaders="/usr/src/sys/"
  ccheaders=`$CC -print-file-name=include`
  dnl We don't have to use -nostdinc.  We just want one more directory
  dnl to be used.
  SYSINCLUDES="-I $sysheaders"
fi

# Don't bother trying to generate any glue code to be compatible with the
# existing system library, because we are the only system library.
inhibit_glue=yes

define([LIBC_KFREEBSD_VERSION],[8.3.0])dnl
if test -n "$sysheaders"; then
  OLD_CPPFLAGS=$CPPFLAGS
  CPPFLAGS="$CPPFLAGS $SYSINCLUDES"
fi
define([libc_cv_kfreebsdVER], [libc_cv_kfreebsd]patsubst(LIBC_KFREEBSD_VERSION,[\.]))dnl
AC_CACHE_CHECK(installed kFreeBSD kernel header files, libc_cv_kfreebsdVER, [dnl
AC_EGREP_CPP([eat flaming death], [#include <osreldate.h>
#if !defined(__FreeBSD_kernel_version) && defined(__FreeBSD_version)
#define __FreeBSD_kernel_version __FreeBSD_version
#endif
#if !defined __FreeBSD_kernel_version || __FreeBSD_kernel_version < ]dnl
patsubst(LIBC_KFREEBSD_VERSION,[^\([^.]*\)\.\([^.]*\)\.\([^.]*\)$],dnl
[ (\1 *100000+ \2 *1000+ \3) /* \1.\2.\3 */])[
eat flaming death
#endif],
	     libc_cv_kfreebsdVER='TOO OLD!',
	     libc_cv_kfreebsdVER='LIBC_KFREEBSD_VERSION or later')])
if test "$libc_cv_kfreebsdVER" != 'LIBC_KFREEBSD_VERSION or later'; then
  AC_MSG_ERROR([GNU libc requires kernel header files from
kFreeBSD LIBC_KFREEBSD_VERSION or later to be installed before configuring.
The kernel header files are found usually in /usr/src/sys/; make sure
these directories use files from kFreeBSD LIBC_KFREEBSD_VERSION or later.
This check uses <osreldate.h>, so
make sure that file was built correctly when installing the kernel header
files.  To use kernel headers not from /usr/src/sys/, use the
configure option --with-headers.])
fi

AC_ARG_ENABLE([compatible-utmp],
	AC_HELP_STRING([--disable-compatible-utmp],
		[use a struct utmp which is the same as struct utmpx, as on kFreeBSD, but incompatible with FreeBSD]),
	[enable_utmp_compat=$enableval],
	[enable_utmp_compat=no])
if test "$enable_utmp_compat" = no; then
  utmp_subdir=utmp-utmpx
else
  utmp_subdir=utmp-compat
fi
sysnames="$sysnames sysdeps/unix/bsd/bsd4.4/kfreebsd/$utmp_subdir"

# If the user gave a minimal version number test whether the available
# kernel headers are young enough.  Additionally we have minimal
# kernel versions for some architectures.  If a previous configure fragment
# set arch_minimum_kernel already, let that override our defaults here.
# Note that we presume such a fragment has set libc_cv_gcc_unwind_find_fde
# if appropriate too.
test -n "$arch_minimum_kernel" ||
case "$machine" in
  i386*)
    libc_cv_gcc_unwind_find_fde=yes
    arch_minimum_kernel=8.3.0
    ;;
  x86_64*)
    arch_minimum_kernel=8.3.0
    ;;
  *)
    arch_minimum_kernel=8.3.0
    ;;
esac
if test -n "$minimum_kernel"; then
  changequote(,)
  user_version=$((`echo "$minimum_kernel.0.0.0" | sed 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/\1 \* 65536 + \2 \* 256 + \3/'`))
  arch_version=$((`echo "$arch_minimum_kernel.0.0.0" | sed 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/\1 \* 65536 + \2 \* 256 + \3/'`))
  changequote([,])
  if test $user_version -lt $arch_version; then
    AC_MSG_WARN([minimum kernel version reset to $arch_minimum_kernel])
    minimum_kernel=$arch_minimum_kernel
  fi
else
  if test $arch_minimum_kernel != '8.3.0'; then
    minimum_kernel=$arch_minimum_kernel
  fi
fi

if test -n "$minimum_kernel"; then
  AC_MSG_CHECKING(for kernel header at least $minimum_kernel)
changequote(,)dnl
  hdrnum=`echo "$minimum_kernel.0.0.0" | sed 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/(\1 * 100000 + \2 * 1000 + \3)/'`;
  decnum=`echo "$minimum_kernel.0.0.0" | sed 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/(\1 * 65536 + \2 * 256 + \3)/'`;
  abinum=`echo "$minimum_kernel.0.0.0" | sed 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/\1,\2,\3/'`;
changequote([,])dnl
  AC_EGREP_CPP([eat flaming death], [#include <osreldate.h>
#if !defined(__FreeBSD_kernel_version) && defined(__FreeBSD_version)
#define __FreeBSD_kernel_version __FreeBSD_version
#endif
#if __FreeBSD_kernel_version < $hdrnum
eat flaming death
#endif], libc_minimum_kernel='too old!', libc_minimum_kernel=ok)
  AC_MSG_RESULT($libc_minimum_kernel)
  if test "$libc_minimum_kernel" = ok; then
    AC_DEFINE_UNQUOTED(__KFREEBSD_KERNEL_VERSION, $decnum)
    AC_DEFINE_UNQUOTED(__ABI_TAG_VERSION, $abinum)
  else
    AC_MSG_ERROR([*** The available kernel headers are older than the requested
*** compatible kernel version])
  fi
fi

if test -n "$sysheaders"; then
  CPPFLAGS=$OLD_CPPFLAGS
fi
# The Linux filesystem standard prescribes where to place "essential"
# files.  I.e., when the installation prefix is "/usr" we have to place
# shared library objects and the configuration files on the root partition
# in /lib and /etc.
case "$prefix" in
/usr | /usr/)
  # 64-bit libraries on bi-arch platforms go in /lib64 instead of /lib
  case $machine in
  sparc/sparc64 | x86_64 | powerpc/powerpc64 | s390/s390-64 | \
  mips/mips64/n64/* )
    libc_cv_slibdir="/lib64"
    if test "$libdir" = '${exec_prefix}/lib'; then
      libdir='${exec_prefix}/lib64';
      # Locale data can be shared between 32bit and 64bit libraries
      libc_cv_localedir='${exec_prefix}/lib/locale'
    fi
    ;;
  mips/mips64/n32/* )
    libc_cv_slibdir="/lib32"
    if test "$libdir" = '${exec_prefix}/lib'; then
      libdir='${exec_prefix}/lib32';
      # Locale data can be shared between 32bit and 64bit libraries
      libc_cv_localedir='${exec_prefix}/lib/locale'
    fi
    ;;
  *)
    libc_cv_slibdir="/lib"
    ;;
  esac
  # Allow the user to override the path with --sysconfdir
  if test $sysconfdir = '${prefix}/etc'; then
    libc_cv_sysconfdir=/etc
  else
    libc_cv_sysconfdir=$sysconfdir
   fi
  libc_cv_rootsbindir="/sbin"
  ;;
esac


# Put exception handling support into libc, so that not every shared
# library needs to include it.
# FIXME: Does not work yet.
libc_cv_gcc_unwind_find_fde=no


# Under kFreeBSD the FBTL add-on should be available.
case $add_ons in
  # It is available.  Good.
  *fbtl*)
    fbtl_missing=
    ;;
  *)
    fbtl_missing=yes
    ;;
esac

if test "$fbtl_missing"; then
  if test $enable_sanity = yes; then
    echo "\
*** On GNU/kFreeBSD systems it is normal to compile GNU libc with the
*** \`fbtl' add-on.  Without that, the library will be
*** incompatible with normal GNU/kFreeBSD systems.
*** If you really mean to not use this add-on, run configure again
*** using the extra parameter \`--disable-sanity-checks'."
    exit 1
  else
    echo "\
*** WARNING: Are you sure you do not want to use the \`fbtl'
*** add-on?"
  fi
fi

if test "$prefix" = "/usr/local" -o "$prefix" = "/usr/local/" -o "$prefix" = "NONE"; then
  if test $enable_sanity = yes; then
    echo "\
*** On GNU/kFreeBSD systems the GNU C Library should not be installed into
*** /usr/local since this might make your system totally unusable.
*** We strongly advise to use a different prefix.  For details read the FAQ.
*** If you really mean to do this, run configure again using the extra
*** parameter \`--disable-sanity-checks'."
    exit 1
  else
    echo "\
*** WARNING: Do you really want to install the GNU C Library into /usr/local?
*** This might make your system totally unusable, for details read the FAQ."
  fi
fi


# One kFreeBSD we use ldconfig.
use_ldconfig=yes

# We need some extensions to the `ldd' script.
changequote(,)
case "$machine" in
  x86_64*)
    ldd_rewrite_script=$dir/x86_64/ldd-rewrite.sed
    ;;
  *)
    ;;
esac
changequote([,])

if test $host = $build; then
  # If $prefix/include/{net,scsi} are symlinks, make install will
  # clobber what they're linked to (probably a kernel tree).
  # test -L ought to work on all Linux boxes.
  if test "x$prefix" != xNONE; then
    ac_prefix=$prefix
  else
    ac_prefix=$ac_default_prefix
  fi
  AC_MSG_CHECKING([for symlinks in ${ac_prefix}/include])
  ac_message=
  if test -L ${ac_prefix}/include/net; then
    ac_message="$ac_message
   ${ac_prefix}/include/net is a symlink"
  fi
  if test -L ${ac_prefix}/include/scsi; then
    ac_message="$ac_message
   ${ac_prefix}/include/scsi is a symlink"
  fi
  if test -n "$ac_message"; then
    AC_MSG_ERROR([$ac_message
\`make install' will destroy the target of the link(s).
Delete the links and re-run configure, or better still, move the entire
${ac_prefix}/include directory out of the way.])
  else
    AC_MSG_RESULT(ok)
  fi
fi

# We support internal syscalls.
# It is advertised as inlined syscalls availability ...
AC_DEFINE(HAVE_INLINED_SYSCALLS)
